#include <WiFi.h>
#include <WebServer.h>
#include <Preferences.h>

#define SOFT_AP_SSID "LuxtepConfigAP"
#define SOFT_AP_PASS "1234"

Preferences preferences;
WebServer server(80);

void handleRoot() {
  server.send(200, "text/html", R"rawliteral(
    <html><body>
    <h2>Wi-Fi setting</h2>
    <form action="/save" method="POST">
      SSID: <input type="text" name="ssid"><br>
      PASS: <input type="password" name="pass"><br>
      <input type="submit" value="Save">
    </form>
    </body></html>
  )rawliteral");
}

void handleSave() {
  if (server.method() == HTTP_POST) {
    String ssid = server.arg("ssid");
    String pass = server.arg("pass");

    preferences.begin("wifi", false);
    preferences.putString("ssid", ssid);
    preferences.putString("pass", pass);
    preferences.end();

    server.send(200, "text/html", "<html><body>Saved. Rebooting...</body></html>");
    delay(1500);
    ESP.restart();
  }
}

void startSoftAP() {
  WiFi.softAP(SOFT_AP_SSID, SOFT_AP_PASS);
  IPAddress IP = WiFi.softAPIP();
  Serial.println("SoftAP IP address: " + IP.toString());

  server.on("/", handleRoot);
  server.on("/save", handleSave);
  server.begin();
}

bool tryConnectWiFi() {
  preferences.begin("wifi", true);
  String ssid = preferences.getString("ssid", "");
  String pass = preferences.getString("pass", "");
  preferences.end();

  if (ssid.isEmpty() || pass.isEmpty()) {
    return false;
  }

  WiFi.begin(ssid.c_str(), pass.c_str());
  Serial.printf("Connecting to %s...\n", ssid.c_str());

  for (int i = 0; i < 15; ++i) {
    if (WiFi.status() == WL_CONNECTED) return true;
    delay(500);
    Serial.print(".");
  }
  return false;
}

void setup() {
  Serial.begin(921600);
  if (!tryConnectWiFi()) {
    Serial.println("\nWi-Fi 연결 실패, SoftAP 모드 진입");
    startSoftAP();
  } else {
    Serial.printf("\nWi-Fi 연결 성공: %s\n", WiFi.localIP().toString().c_str());
  }
}

void loop() {
  server.handleClient();
}
